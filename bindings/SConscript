#!/usr/bin/env python
import os
import sys
from distutils.sysconfig import get_python_lib, get_python_inc

Import('env')
buildenv = env.Clone()

# First check we're using the correct python version
version = sys.version_info
if version[0] == 2 and version[1] < 7:
    print('!! OXSX python bindings require python 2.7')
    Exit(0)

# Have to include all cpp files (unless we add to the list each time)
buildenv.Append(LIBPATH = '#/build')
buildenv.Prepend(LIBS = ['oxsx'])
if "RPATH" in env and env["RPATH"] != "" and env["SYSTEM"] == "Darwin":
    buildenv.Append(LINKFLAGS = "-rpath " + ",".join(str(l) for l in env["RPATH"]))
buildenv.Append(CPPPATH = '#/src')
buildenv.Append(CPPPATH = [get_python_inc(), '#/pybind11/include'])
buildenv.Append(CXXFLAGS = ['-std=c++11'])

shlib = buildenv.SharedLibrary('oxsx', ['oxsx_pybind.cpp'],
                               SHLIBSUFFIX='.so', SHLIBPREFIX="")

# Install to pythonpath
install_dir = get_python_lib()
install = Install(install_dir, shlib)

env.Alias("bindings", [shlib, install])

