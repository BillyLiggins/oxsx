#!/usr/bin/env python
import os
from distutils.sysconfig import get_python_lib

Import('env')
buildenv = env.Clone()

# Have to include all cpp files (unless we add to the list each time)
buildenv.Append(LIBPATH = '#/build')
buildenv.Append(LIBS = 'oxsx')
if "RPATH" in env and env["RPATH"] != "":
    buildenv.Append(LINKFLAGS = "-rpath " + ",".join(str(l) for l in env["RPATH"]))
buildenv.Append(CPPPATH = '#/src')
buildenv.Append(CPPPATH = '#/pybind11/include')
buildenv.Append(CXXFLAGS = ' -std=c++0x')
shlib = buildenv.SharedLibrary('oxsx', ['oxsx_pybind.cpp'],
                               SHLIBSUFFIX='.so', SHLIBPREFIX='')

# Install to pythonpath
install_dir = get_python_lib()
install = Install(install_dir, shlib)

env.Alias("bindings", [shlib, install])

