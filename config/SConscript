#!/bin/python
# SConscript responsible for setting up the build environment using the dependencies
import buildhelp
import platform
import os

env = Environment(CXXFLAGS = "-O2")
env["SYSTEM"] = platform.system()

if env.GetOption('clean'):
    Return('env')

conf = Configure(env, log_file = "/dev/null")   
# sanity check      
buildhelp.sanity_checks(conf)

# first load the dependencies from the ini
dependencies = buildhelp.read_dependencies("dependencies.ini")

# now overwrite any options from custom build
if FindFile("userconfig.ini", "."):
    compiler = buildhelp.parse_user_config("userconfig.ini", dependencies)        
else:
    compiler = None # default
    
if compiler:
    env.Replace(CXX = compiler)

# for darwin, fix the install paths for a copy of gsl & armadillo libs,
# on linux just leave it alone. 
cpys = [Command(**kw) for kw in buildhelp.create_cpy_commands(conf, dependencies, "gsl", "#/gsl")]
cpys += [Command(**kw) for kw in buildhelp.create_cpy_commands(conf, dependencies, "armadillo", "#/armadillo")]

conf.env.Default(cpys)

# update the environment, checking along the way
buildhelp.update_and_check_env(conf, dependencies)
env = conf.Finish()
Return('env')
